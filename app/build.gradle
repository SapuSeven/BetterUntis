apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlinx-serialization'

android {
	compileSdkVersion 31
	buildToolsVersion '31.0.0'

	defaultConfig {
		applicationId "com.sapuseven.untis"
		minSdkVersion 21
		targetSdkVersion 31
		versionCode 32
		versionName "3.4.8"
		vectorDrawables.useSupportLibrary = true
		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
	}

	signingConfigs {
		def propertiesFile = file("signing.properties")

		ext.signingProperties = new Properties()
		if (propertiesFile.exists()) {
			propertiesFile.withInputStream {
				stream -> signingProperties.load(stream)
			}
		}

		if (file("BetterUntis.jks").exists()) {
			release {
				storeFile file("BetterUntis.jks")
				storePassword signingProperties["keystorePassword"] != null ? signingProperties["keystorePassword"] : System.getenv("KEYSTORE_PASSWORD")
				keyAlias "release"
				keyPassword signingProperties["keyReleasePassword"] != null ? signingProperties["keyReleasePassword"] : System.getenv("KEY_RELEASE_PASSWORD")
			}

			debug {
				storeFile file("BetterUntis.jks")
				storePassword signingProperties["keystorePassword"] != null ? signingProperties["keystorePassword"] : System.getenv("KEYSTORE_PASSWORD")
				keyAlias "debug"
				keyPassword signingProperties["keyDebugPassword"] != null ? signingProperties["keyDebugPassword"] : System.getenv("KEY_DEBUG_PASSWORD")
			}
		}
	}

	buildTypes {
		release {
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

			if (file("BetterUntis.jks").exists()) {
				signingConfig signingConfigs.release
			}
		}

		debug {
			minifyEnabled false
			shrinkResources false
			applicationIdSuffix '.debug'
			versionNameSuffix '-DEBUG'

			if (file("BetterUntis.jks").exists()) {
				signingConfig signingConfigs.debug
			}
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	kotlinOptions {
		jvmTarget = JavaVersion.VERSION_1_8.toString()
	}

	lintOptions {
		disable "MissingTranslation"
	}
}

dependencies {
	implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
	implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
	implementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
	implementation "org.jetbrains.kotlinx:kotlinx-serialization-cbor:$serialization_version"
	implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$serialization_version"
	implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.8-1.4.0-rc"
	implementation 'androidx.appcompat:appcompat:1.1.0'
	implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta8'
	implementation 'androidx.fragment:fragment-ktx:1.2.4'
	implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"
	implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01'
	implementation 'androidx.recyclerview:recyclerview:1.1.0'
	implementation "androidx.preference:preference-ktx:1.1.1"
	implementation "com.github.kittinunf.fuel:fuel:2.2.3"
	implementation "com.github.kittinunf.fuel:fuel-coroutines:2.2.3"
	implementation "joda-time:joda-time:2.10.6"
	implementation 'com.google.android.material:material:1.2.0-alpha04' //Newer versions will break DatePickerDialog
	implementation 'com.jaredrummler:colorpicker-compat:1.0.5'
	implementation 'com.github.cesarferreira:MaterialComponent.Banner:0.13.0'
	implementation 'com.budiyev.android:code-scanner:2.1.0'
	implementation 'ca.antonious:materialdaypicker:0.6.0'
	testImplementation 'junit:junit:4.13'
	testImplementation 'org.mockito:mockito-core:4.1.0'
	androidTestImplementation 'androidx.test:runner:1.3.0'
	androidTestImplementation 'androidx.test:rules:1.3.0'
	androidTestImplementation 'androidx.test.ext:junit:1.1.2'
	androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
	androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
	androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
	androidTestImplementation 'tools.fastlane:screengrab:2.0.0'

	implementation project(':weekview')
}
