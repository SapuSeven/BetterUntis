plugins {
	id 'com.android.application'
	id 'io.sentry.android.gradle' version '3.4.2'
	id 'com.mikepenz.aboutlibraries.plugin'
	id 'de.mannodermaus.android-junit5'
	id 'com.google.devtools.ksp'
	id 'com.google.dagger.hilt.android'
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlinx-serialization'

// Auto-generates a new version code every minute
static def generateVersionCode() {
	return (new Date().getTime() / 1000 / 60).toInteger()
}

android {
	compileSdkVersion 34
	namespace 'com.sapuseven.untis'

	androidResources {
		generateLocaleConfig true
	}

	defaultConfig {
		applicationId "com.sapuseven.untis"
		minSdkVersion 21
		targetSdkVersion 33
		versionCode generateVersionCode()
		versionName "4.1.0-beta01"
		vectorDrawables.useSupportLibrary = true
		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

		buildConfigField "String", "SENTRY_DSN", '"https://d3b77222abce4fcfa74fda2185e0f8dc@o1136770.ingest.sentry.io/6188900"'

		ksp {
			arg("room.schemaLocation", "$projectDir/schemas".toString())
		}
	}

	signingConfigs {
		def propertiesFile = file("signing.properties")

		ext.signingProperties = new Properties()
		if (propertiesFile.exists()) {
			propertiesFile.withInputStream {
				stream -> signingProperties.load(stream)
			}
		}

		if (file("BetterUntis.jks").exists()) {
			release {
				storeFile file("BetterUntis.jks")
				storePassword signingProperties["keystorePassword"] != null ? signingProperties["keystorePassword"] : System.getenv("KEYSTORE_PASSWORD")
				keyAlias "release"
				keyPassword signingProperties["keyReleasePassword"] != null ? signingProperties["keyReleasePassword"] : System.getenv("KEY_RELEASE_PASSWORD")
			}

			debug {
				storeFile file("BetterUntis.jks")
				storePassword signingProperties["keystorePassword"] != null ? signingProperties["keystorePassword"] : System.getenv("KEYSTORE_PASSWORD")
				keyAlias "debug"
				keyPassword signingProperties["keyDebugPassword"] != null ? signingProperties["keyDebugPassword"] : System.getenv("KEY_DEBUG_PASSWORD")
			}
		}
	}

	buildTypes {
		release {
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

			if (file("BetterUntis.jks").exists()) {
				signingConfig signingConfigs.release
			}
		}

		debug {
			minifyEnabled false
			shrinkResources false
			applicationIdSuffix '.debug'
			versionNameSuffix '-DEBUG'

			if (file("BetterUntis.jks").exists()) {
				signingConfig signingConfigs.debug
			}
		}
	}

	buildFeatures {
		compose true
	}

	compileOptions {
		coreLibraryDesugaringEnabled true

		sourceCompatibility JavaVersion.VERSION_17
		targetCompatibility JavaVersion.VERSION_17
	}

	kotlinOptions {
		jvmTarget = JavaVersion.VERSION_17.toString()
	}

	composeOptions {
		kotlinCompilerExtensionVersion "$kotlin_compiler_ext_version"
	}

	lint {
		disable 'MissingTranslation'
	}

	flavorDimensions "dependencies"
	productFlavors {
		gms {
			isDefault true
			dimension "dependencies"
		}

		foss {
			dimension "dependencies"
		}
	}
}

sentry {
	autoUploadProguardMapping = System.getenv('SENTRY_PROJECT') != null
}

aboutLibraries {
	includePlatform = false
	duplicationMode = com.mikepenz.aboutlibraries.plugin.DuplicateMode.MERGE
}

dependencies {
	implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
	implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
	implementation "org.jetbrains.kotlinx:kotlinx-serialization-cbor:$serialization_version"
	implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$serialization_version"
	implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
	implementation 'androidx.appcompat:appcompat:1.4.2'
	implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
	implementation 'androidx.fragment:fragment-ktx:1.5.0'
	implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.0'
	implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01'
	implementation 'androidx.recyclerview:recyclerview:1.2.1'
	implementation 'androidx.preference:preference-ktx:1.2.0'
	implementation "com.github.kittinunf.fuel:fuel:$fuel_version"
	implementation "com.github.kittinunf.fuel:fuel-coroutines:$fuel_version"
	implementation "com.github.kittinunf.fuel:fuel-kotlinx-serialization:$fuel_version"
	implementation 'joda-time:joda-time:2.10.14'
	implementation 'com.google.android.material:material:1.7.0-alpha03'
	implementation 'com.github.ajalt.colormath:colormath:3.2.0'
	implementation 'com.journeyapps:zxing-android-embedded:4.3.0'
	implementation 'androidx.activity:activity-compose:1.6.1'
	implementation "androidx.compose.animation:animation:$compose_version"
	implementation "androidx.compose.ui:ui:$compose_version"
	implementation "androidx.compose.ui:ui-tooling:$compose_version"
	implementation 'androidx.compose.material3:material3:1.1.2'
	implementation 'androidx.glance:glance-appwidget:1.0.0-alpha05'
	implementation 'androidx.navigation:navigation-compose:2.5.0'
	implementation 'androidx.datastore:datastore-preferences:1.0.0'
	implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.0'
	implementation "androidx.room:room-runtime:$room_version"
	implementation "androidx.room:room-ktx:$room_version"
	implementation 'com.google.android.material:material:1.6.1'
	implementation 'com.google.android.material:compose-theme-adapter:1.1.14'
	implementation "com.google.accompanist:accompanist-swiperefresh:$accompanist_version"
	implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"
	implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
	implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist_version"
	implementation "com.google.accompanist:accompanist-flowlayout:$accompanist_version"
    implementation 'androidx.work:work-runtime-ktx:2.7.1'
	implementation 'io.sentry:sentry-android:6.33.1'
	implementation 'io.sentry:sentry-compose-android:6.33.1'
	implementation 'io.coil-kt:coil-compose:2.2.2'
	implementation "com.mikepenz:aboutlibraries-core:$aboutlibraries_version"
	implementation "com.mikepenz:aboutlibraries-compose:$aboutlibraries_version"
	implementation "io.ktor:ktor-client-cio:$ktor_version"
	implementation "com.google.dagger:hilt-android:$dagger_version"
	gmsImplementation 'com.google.android.gms:play-services-code-scanner:16.0.0'

	coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

	ksp "androidx.room:room-compiler:$room_version"
	ksp "com.google.dagger:dagger-compiler:$dagger_version"
	ksp "com.google.dagger:hilt-compiler:$dagger_version"

	testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
	testImplementation "org.junit.jupiter:junit-jupiter-params:$junit_version"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
	testRuntimeOnly "org.junit.vintage:junit-vintage-engine:$junit_version"

	testImplementation 'junit:junit:4.13.2'
	testImplementation 'io.mockk:mockk:1.13.4'
	testImplementation 'org.mockito:mockito-core:4.6.1'
	testImplementation 'org.hamcrest:hamcrest:2.2'
	testImplementation 'org.hamcrest:hamcrest-library:2.2'
	androidTestImplementation 'androidx.test:core:1.5.0-alpha02' // Version 1.5.0-alpha02 fixes crash on Android 13 - see https://github.com/android/android-test/issues/1412
	androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
	debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"

	implementation project(':api')
	implementation project(':weekview')
	implementation project(':material-color-utils')
}
